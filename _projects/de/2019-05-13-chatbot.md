---
layout: project_page
name: "Telegram Chatbot für die senseBox"
date: 2019-05-13
author: Jan
abstract: "Tutorial zum Steuern der senseBox MCU über die UniversalTelegramBot Bibliothek für Arduino"
image1: /images/projects/telegram_bot/01_botfather.png
image2: /images/projects/telegram_bot/02_botname.png
image4: /images/projects/telegram_bot/03_botcreated.png
image5: /images/projects/telegram_bot/04_botcommunication.png
image6: /images/projects/telegram_bot/05_ledonoff.png
image7: /images/projects/telegram_bot/06_serialout.png
image8: /images/projects/telegram_bot/07_sensorstatus.png
image9: /images/projects/telegram_bot/08_customkeyboard.png
material:
    - senseBox MCU
    - WiFi Bee WINC1500
    - HDC1080 Sensor (über I2C auf 0x40)
    - senseBox JST Kabel
ide: arduino
lang: de
tags: ["IoT","Chatbot"]
difficult: fortgeschritten
---
# Telegram Chatbot für die senseBox
Beim Recherchieren bin ich auf eine Arduino Bibliothek gestoßen die Teile der HTTP-basierten Telegram Bot API implementiert. Damit lassen sich also einfache Chatbots erstellen die Arduino-basierte Internet of Things Anwendungen, wie unsere senseBox, um coole Funktionen erweitern. In diesem Projekt lernt ihr die Funktionen dieser Library an den richtigen Stellen zu bearbeiten um über den Chatbot am Controller angeschlossene Aktoren zu steuern, oder Sensoren auszulesen. 

## Aufbau
Der Aufbau ist sehr simpel und besteht am Anfang aus der MCU mit WiFi Bee auf Steckplatz XBEE1. Später kommt dann noch unser HDC1080 Sensor an I2C dazu.

## Programmierung
Für dieses Projekt solltet ihr euch mindestens mit den ersten Schritten aus der senseBox Dokumentation auskennen. Euer Rechner sollte dementsprechend so konfiguriert sein, dass ihr die senseBox MCU über Arduino programmieren könnt (siehe Bereich „Erste Schritte“ in Materialbereich). Außerdem werden Grundlagen zur Implementierung eines Sensors vorausgesetzt.

### Benötigte Bibliotheken installieren
Startet die Arduino IDE und öffnet dort den Bibliotheksverwalter unter Sketch -> Bibliothek einbinden -> Bibliotheken verwalten… (oder Shortcut Strg + Shift + i). 
Sucht und installiert dort die beiden Bibliotheken:
1.  [UniversalTelegramBot](https://github.com/witnessmenow/Universal-Arduino-Telegram-Bot) von Brian Lough (Version 1.1.0 oder höher)
2.  [ArduinoJson](https://github.com/bblanchon/ArduinoJson) von Benoit Blanchon (Version 5.13.5; **nicht Version 6 oder höher!**)

### Telegram-Bot erstellen
Falls ihr den Telegram Messenger noch nicht besitzt, installiert ihn auf eurem Smartphone oder Arbeitsrechtner (https://telegram.org/apps).
Startet Telegram, sucht über das Eingabefeld nach *@BotFather* und startet den Chat mit ihm. *@BotFather* ist selbst ein Bot und wird genutzt um eigene Bots zu erstellen oder zu bearbeiten.

{% include image.html image=page.image1 %}

Erstellt nun mit dem `/newbot` Befehl im Chat einen neuen Bot und gebt ihm einen Namen.

{% include image.html image=page.image2 %}

Als nächstes braucht euer Bot noch einen eindeutigen Nutzernamen (username). Wir wählen hier senseBoxMCU_testBot.

{% include image.html image=page.image3 %}

Wenn ihr einen Namen gefunden habt der noch nicht vergeben ist bekommt ihr eine Bestätigung das alles geklappt hat, wie oben dargestellt. Wichtige Informationen sind einmal der Chatlink zu eurem Bot, sowie den rot gedruckten Access Token. Letzteren müsst ihr kopieren und gleich im Anwendungsbeispiel einfügen.

### Beispiel 1: LED steuern
Als erstes wollen wir versuchen die rote Onboard-LED auf unserem Controller über den Bot zu steuern. Als Grundlage nutzen wir dazu das FlashLedBot Beispiel aus der UniversalTelegramBot Library. Startet also die Arduino IDE und öffnet das passende Beispiel, oder kopiert den folgenden Sketch in das Arduino Textfeld. Wir passen zunächst lediglich den WLAN Zugang, den Access Token und die Nummer des LED-Pins an:

```C
 #include <WiFiSSLClient.h>
 #include <WiFi101.h>
 #include <UniversalTelegramBot.h>

// Initialize Wifi connection to the router
char ssid[] = "senseBox-net";              // your network SSID (name)
char password[] = "senseBox-pw";           // your network key

const int ledPin = 7; //red LED onboard senseBoxMCU

// Initialize Telegram BOT
#define BOTtoken "879449240:AAGxBFMQvDpd50lQNQlYrLlmf5bDz1aXPsk"  // your Bot Token (Get from Botfather)

WiFiSSLClient client;
UniversalTelegramBot bot(BOTtoken, client);

int Bot_mtbs = 1000; //mean time between scan messages
long Bot_lasttime;   //last time messages' scan has been done
bool Start = false;
int ledStatus = 0;

void handleNewMessages(int numNewMessages) {
  Serial.println("handleNewMessages");
  Serial.println(String(numNewMessages));
  for(int i=0; i<numNewMessages; i++) {
    String chat_id = String(bot.messages[i].chat_id);
    String text = bot.messages[i].text;
    if (text == "/ledon") {
      digitalWrite(ledPin, HIGH);   // turn the LED on (HIGH is the voltage level)
      ledStatus = 1;
      bot.sendMessage(chat_id, "Led is ON", "");
    }
    if (text == "/ledoff") {
      ledStatus = 0;
      digitalWrite(ledPin, LOW);    // turn the LED off (LOW is the voltage level)
      bot.sendMessage(chat_id, "Led is OFF", "");
    }
    if (text == "/status") {
      if(ledStatus){
        bot.sendMessage(chat_id, "Led is ON", "");
      } else {
        bot.sendMessage(chat_id, "Led is OFF", "");
      }
    }
    if (text == "/start") {
      String from_name = bot.messages[i].from_name;
      if (from_name == "") from_name = "Anonymous";

      String welcome = "Welcome, " + from_name + "! This is your fancy senseBoxMCU bot \xF0\x9F\x91\xBD\n\n";
      welcome = welcome + "Use the following commands to control the red onboard LED:\n"; 
      welcome = welcome + "/ledon : to switch the Led ON \n"; 
      welcome = welcome + "/ledoff : to switch the Led OFF \n";
      welcome = welcome + "/status : Returns current status of LED \n";
      bot.sendMessage(chat_id, welcome, "Markdown");
    }
  }
}

void setup() {
  Serial.begin(9600);
  //while(!Serial);
  delay(2000);

  // attempt to connect to Wifi network:
  Serial.print("Connecting Wifi: ");
  Serial.println(ssid);
  while (WiFi.begin(ssid, password) != WL_CONNECTED) {
    Serial.print(".");
    delay(500);
  }
  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  IPAddress ip = WiFi.localIP();
  Serial.println(ip);

  pinMode(ledPin, OUTPUT); // initialize digital ledPin as an output.
  delay(10);
  digitalWrite(ledPin, LOW); //initialize pin as off
}

void loop() {

  if (millis() > Bot_lasttime + Bot_mtbs)  {
    int numNewMessages = bot.getUpdates(bot.last_message_received + 1);
    while(numNewMessages) {
      Serial.println("got response");
      handleNewMessages(numNewMessages);
      numNewMessages = bot.getUpdates(bot.last_message_received + 1);
    }
    Bot_lasttime = millis();
  }
}

```

Jetzt ladet den Code hoch und wechselt zu Telegram. Hier könnt ihr nun mit dem Bot chatten indem ihr seinen Kontakt auswählt. Mit dem Befehl `/start` bekommt ihr eine Übersicht über die verfügbaren Befehle:

{% include image.html image=page.image4 %}

Wenn ihr nun `/ledon` (bzw. `/ledoff`) eingebt, könnt ihr die rote LED auf der Platine neben dem USB-Eingang an- (oder aus-) schalten.

{% include image.html image=page.image5 %}

Im seriellen Monitor solltet ihr eine Benachrichtigung bekommen jedes Mal, wenn der Bot eine Nachricht bekommt. 

{% include image.html image=page.image6 %}

Schauen wir uns im Sketch die Funktion `handleNewMessages(…)` ab Zeile 23 an sehen wir, dass dort alle verfügbaren Kommandos die der Bot kennt definiert werden. Man kann hier die bekannten Kommandos ändern bzw. neu erstellen oder löschen. 

Ein paar der Kommandos haben darüber hinaus zusätzliche Funktionen. `/start` wird beispielsweise automatisch am Anfang aufgerufen, wenn der Bot zum ersten Mal kontaktiert wird. Ihr könnt das testen indem ihr den Chatverlauf mit dem Bot löscht und dann den Chat neu startet.

### Beispiel 2: Sensor auslesen
Als nächstes Beispiel wollen wir den Wert eines Sensors im Chat abfragen. Hierbei soll uns die Temperatur und Luftfeuchte ausgegeben werden, sobald wir den Bot nach dem `/status` fragen. Dazu schließen wir den senseBox HDC1080 Sensor an den I2C Port an und ergänzen das Beispiel mit den Funktionen um den Sensor auszulesen. Den `/status` Befehl definieren wir in handleNewMessages() entsprechend neu: 

```C
#include <WiFiSSLClient.h>
#include <WiFi101.h>
#include <UniversalTelegramBot.h>
#include <Adafruit_HDC1000.h>

// Initialize Wifi connection to the router
char ssid[] = "senseBox-net";              // your network SSID (name)
char password[] = "senseBox-pw";           // your network key

// Initialize Telegram BOT
#define BOTtoken "879449240:AAGxBFMQvDpd50lQNQlYrLlmf5bDz1aXPsk"  // your Bot Token (Get from Botfather)

WiFiSSLClient client;
UniversalTelegramBot bot(BOTtoken, client);

int Bot_mtbs = 1000; //mean time between scan messages
long Bot_lasttime;   //last time messages' scan has been done
bool Start = false;

Adafruit_HDC1000 hdc;
float temp=0, humi=0;

void handleNewMessages(int numNewMessages) {
  Serial.println("handleNewMessages");
  Serial.println(String(numNewMessages));
  for(int i=0; i<numNewMessages; i++) {
    String chat_id = String(bot.messages[i].chat_id);
    String text = bot.messages[i].text;
    String response;

    if (text == "/status") {
      temp = hdc.readTemperature();
      humi = hdc.readHumidity();
      response = "The temperature is ";
      response += String(temp);
      response += "°C at ";
      response += String(humi);
      response += "% humidity";
      
      if(temp > 25 && temp <= 124){
        response += " \xF0\x9F\x94\xA5";
      } else if (temp < 5){
        response += " \xE2\x9D\x84";
      } else if (temp > 124) { 
        response = "Error reading sensor. Check if sensor is connected or replace cable/sensor.";
      } else {
        response += ".";
      }
      bot.sendMessage(chat_id, response, "");
    }
    if (text == "/start") {
      String from_name = bot.messages[i].from_name;
      if (from_name == "") from_name = "Anonymous";

      String welcome = "Welcome, " + from_name + "! This is your fancy senseBoxMCU bot \xF0\x9F\x91\xBD\n\n";
      welcome = welcome + "Use /status to read HDC1080 sensor measurements.\n";
      bot.sendMessage(chat_id, welcome, "Markdown");
    }
  }
}

void setup() {
  Serial.begin(9600);
  //while(!Serial);
  delay(2000);

  // attempt to connect to Wifi network:
  Serial.print("Connecting Wifi: ");
  Serial.println(ssid);
  while (WiFi.begin(ssid, password) != WL_CONNECTED) {
    Serial.print(".");
    delay(500);
  }
  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  IPAddress ip = WiFi.localIP();
  Serial.println(ip);

  hdc.begin(0x40);
  temp = hdc.readTemperature();
  humi = hdc.readHumidity();
}

void loop() {

  if (millis() > Bot_lasttime + Bot_mtbs)  {
    int numNewMessages = bot.getUpdates(bot.last_message_received + 1);
    while(numNewMessages) {
      Serial.println("got response");
      handleNewMessages(numNewMessages);
      numNewMessages = bot.getUpdates(bot.last_message_received + 1);
    }
    Bot_lasttime = millis();
  }
}
```

Wenn ihr den Code auf die MCU ladet könnt ihr nun den Befehl `/status` nutzen um euch Temperatur und Luftfeuchte im Chatfenster ausgeben zu lassen.

{% include image.html image=page.image7 %}

### Beispiel 3: Custom Keyboard erstellen
Eine weitere interessante Funtion des Chatbots welche die UniversalTelegramBot Bibliothek bereitstellt ist das Custom Keyboard. Um das zu zeigen, fügen wir die Beiden oberen Beispiele zusammen. Dazu definieren wir erneut Kommandos für /ledon, /ledoff und /readSensor. Beim /start Befehl ersetzen wir nun den bot.sendMessage(…) Aufruf mit bot.sendMessageWithReplyKeyboard(…) um beim Start eine Tastatur mit unseren drei Funktionen zu erzeugen.

```C
void handleNewMessages(int numNewMessages) {
  Serial.println("handleNewMessages");
  Serial.println(String(numNewMessages));
  String response;
  for(int i=0; i<numNewMessages; i++) {
    String chat_id = String(bot.messages[i].chat_id);
    String text = bot.messages[i].text;
    if (text == "/ledon") {
      digitalWrite(ledPin, HIGH);   // turn the LED on (HIGH is the voltage level)
      ledStatus = 1;
      bot.sendMessage(chat_id, "Led is ON", "");
    }
    if (text == "/ledoff") {
      ledStatus = 0;
      digitalWrite(ledPin, LOW);    // turn the LED off (LOW is the voltage level)
      bot.sendMessage(chat_id, "Led is OFF", "");
    }
    if (text == "/readSensor") {
      temp = hdc.readTemperature();
      humi = hdc.readHumidity();
      response = "The temperature is ";
      response += String(temp);
      response += "°C at ";
      response += String(humi);
      response += "% humidity";
      
      if(temp > 25 && temp <= 124){
        response += " \xF0\x9F\x94\xA5";
      } else if (temp < 5){
        response += " \xE2\x9D\x84";
      } else if (temp > 124) { 
        response = "Error reading sensor. Check if sensor is connected or replace cable/sensor.";
      } else {
        response += ".";
      }
      bot.sendMessage(chat_id, response, "");
    }
    if (text == "/start") {
      String keyboardJson = "[[\"/ledon\", \"/ledoff\"],[\"/readSensor\"]]";
      bot.sendMessageWithReplyKeyboard(chat_id, "Select a function from the keyboard", "", keyboardJson, true);
    }
  }
}
```

Beim Start wird nun nicht mehr die Übersicht der Kommandos, sondern das Custom Keyboard angezeigt. Ihr könnt es auch manuell über `/start` aufrufen.

{% include image.html image=page.image8 %}

Mit einem Klick auf die Tastatur wird nun der entsprechende Befehl ausgeführt.

### Weitere Tipps
* Eine Übersicht über alle Befehle aus der Bibliothek findet ihr in der Dokumentation (https://github.com/witnessmenow/Universal-Arduino-Telegram-Bot). 

* Um mit dem Bot Emojis über die Lib zu verschicken müsst ihr die UTF-8 Notation verwenden (https://apps.timwhitlock.info/emoji/tables/unicode). 
